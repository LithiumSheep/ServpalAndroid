buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


@SuppressWarnings("GrMethodMayBeStatic")
def gitBranch() {
    def branch = ''
    def proc = 'git rev-parse --abbrev-ref HEAD'.execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.1"
    defaultConfig {
        applicationId "com.servpal.android"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 2
        versionName "0.2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {   // currently using debug.keystore
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file(System.getProperty("user.home").toString() + '/.android/debug.keystore')
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix ' DEBUG (' + gitBranch() + ')'
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    flavorDimensions "api"
    productFlavors {
        dev {
            dimension "api"
            buildConfigField "boolean", "DEV", "true"
        }
        prod {
            dimension "api"
            buildConfigField "boolean", "DEV", "false"
        }
    }
    // adds Java 8 source compatibility
    // https://developer.android.com/studio/write/java8-support.html
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true;
    }

    // For SharedPreferences inspection.  Go to chrome://inspect and inspect device
    //implementation 'com.facebook.stetho:stetho:1.5.0'

    // support libs
    implementation 'com.android.support:support-v4:27.0.1'
    implementation 'com.android.support:appcompat-v7:27.0.1'
    implementation 'com.android.support:design:27.0.1'
    implementation 'com.android.support:customtabs:27.0.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // TODO: google-services libs
//    implementation 'com.google.android.gms:play-services-analytics:11.6.0'  // analytics
//    implementation 'com.google.android.gms:play-services-gcm:11.6.0' // GCM or FCM?
//    implementation 'com.google.android.gms:play-services-location:11.6.0'   // location

    // networking
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation('com.github.ihsanbal:LoggingInterceptor:2.0.2') {
        exclude group: 'org.json', module: 'json'
    }

    // utils
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'org.parceler:parceler-api:1.1.9'
    annotationProcessor 'org.parceler:parceler:1.1.9'

    // date/time
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
}
